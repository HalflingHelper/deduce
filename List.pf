import Nat

union List<T> {
  empty;
  node(T, List<T>);
}

function length<E>(List<E>) -> Nat {
  length(empty) = 0;
  length(node(n, next)) = 1 + length(next);
}

function append<E>(List<E>, List<E>) -> List<E> {
  append(empty, ys) = ys;
  append(node(n, xs), ys) = node(n, append(xs, ys));
}

function reverse<E>(List<E>) -> List<E> {
  reverse(empty) = empty;
  reverse(node(n, next)) = append(reverse(next), node(n, empty));
}

theorem length_zero: length(empty) = 0
proof
  reflexive
end

theorem length_one: all U :type, x:U. length(node(x, empty)) = 1
proof
  arbitrary U:type, x:U;
  reflexive
end

define L1 = node(1, node(2, empty))
define L2 = node(3, node(4, node(5, empty)))
define L3 = node(1, node(2, node(3, node(4, node(5, empty)))))

theorem length_L3: length(L3) = 5
proof
  reflexive
end

theorem append_test: append(L1, L2) = L3
proof
  reflexive
end

theorem reverse_test2: reverse(L1) = node(2, node(1, empty))
proof
  reflexive
end

theorem reverse_test3: reverse(L2) = node(5, node(4, node(3, empty)))
proof
  reflexive
end

theorem length_append_123: length(append(L1, L2)) = length(L1) + length(L2)
proof
  reflexive
end  

theorem length_append: all U :type. all xs :List<U>. all ys :List<U>.
  length(append(xs, ys)) = length(xs) + length(ys)
proof
  arbitrary U :type;
  induction List<U>
  case empty {
    arbitrary ys:List<U>;
    have length(append(empty,ys)) = length(empty) + length(ys) by reflexive
  }
  case node(n, xs') {
    arbitrary ys :List<U>;
    equations
      length(append(node(n,xs'),ys))
          = length(node(n, append(xs', ys)))    by reflexive
      ... = 1 + length(append(xs', ys))         by reflexive
      ... = 1 + (length(xs') + length(ys))
                              by rewrite goal with IH[ys]; reflexive
      ... = length(node(n,xs')) + length(ys)    by reflexive
  }
end

theorem append_assoc1:
  append(append(node(1, empty), node(2, empty)), node(3, empty))
  = append(node(1, empty), append(node(2, empty), node(3, empty)))
proof
  reflexive
end  

theorem append_assoc: all U :type. all xs :List<U>. all ys :List<U>, zs:List<U>.
  append(append(xs, ys), zs) = append(xs, append(ys, zs))
proof
  arbitrary U :type;
  induction List<U>
  case empty {
    arbitrary ys :List<U>, zs :List<U>;
    have append(append(empty,ys),zs) = append(empty,append(ys,zs)) by reflexive
  }
  case node(n, xs') {
    arbitrary ys :List<U>, zs :List<U>;
    equations
      append(append(node(n,xs'),ys),zs)
          = node(n,append(append(xs',ys),zs))         by reflexive
      ... = append(node(n,xs'),append(ys,zs))
                                  by rewrite goal with IH[ys,zs]; reflexive
  }
end

theorem append_empty: all U :type. all xs :List<U>.
  append(xs, empty) = xs
proof
  arbitrary U :type;
  induction List<U>
  case empty {
    have append(empty, empty) = empty by reflexive
  }
  case node(n, xs') {
    equations
      append(node(n,xs'),empty)
          = node(n,append(xs',empty))     by reflexive
      ... = node(n,xs')                   by rewrite goal with IH; reflexive
  }
end

theorem length_reverse: all U :type. all xs :List<U>.
  length(reverse(xs)) = length(xs)
proof
  arbitrary U : type;
  induction List<U>
  case empty {
    have length(reverse(empty)) = length(empty) by reflexive
  }
  case node(n, xs') {
    equations
      length(reverse(node(n,xs')))
          = length(append(reverse(xs'), node(n,empty))) by reflexive
      ... = length(reverse(xs')) + length(node(n,empty))
                    by rewrite goal with
                      length_append[U][reverse(xs')][node(n,empty)]; reflexive
      ... = length(xs') + 1           by rewrite goal with IH; reflexive
      ... = length(node(n,xs'))
                       by rewrite goal with add_one[length(xs')]; reflexive
  }
end

theorem reverse_append_123:
  reverse(append(L1, L2)) = append(reverse(L2), reverse(L1))
proof
  reflexive
end  

theorem reverse_append: all U :type. all xs :List<U>. all ys :List<U>.
  reverse(append(xs, ys)) = append(reverse(ys), reverse(xs))
proof
  arbitrary U :type;
  induction List<U>
  case empty {
    arbitrary ys :List<U>;
    equations
    reverse(append(empty,ys))
        = reverse(ys)                          by reflexive
    ... = append(reverse(ys),empty)
                by rewrite goal with append_empty[U][reverse(ys)]; reflexive
    ... = append(reverse(ys),reverse(empty))   by reflexive
  }
  case node(n, xs') {
    arbitrary ys :List<U>;
    equations
    reverse(append(node(n,xs'),ys))
        = reverse(node(n,append(xs',ys))) by reflexive
    ... = append(reverse(append(xs',ys)), node(n, empty)) by reflexive
    ... = append(append(reverse(ys), reverse(xs')), node(n, empty))
          by rewrite goal with IH[ys]; reflexive
    ... = append(reverse(ys), append(reverse(xs'), node(n, empty)))
          by append_assoc[U][reverse(ys)][reverse(xs'), node(n, empty)]
    ... = append(reverse(ys), reverse(node(n,xs'))) by reflexive
  }
end

theorem reverse_reverse: all U :type. all ls :List<U>.
  reverse(reverse(ls)) = ls
proof
  arbitrary U :type;
  induction List<U>
  case empty {
    have reverse(reverse(empty)) = empty by reflexive
  }
  case node(n, ls') {
    equations
      reverse(reverse(node(n,ls')))
          = reverse(append(reverse(ls'), node(n,empty))) by reflexive
      ... = append(reverse(node(n,empty)), reverse(reverse(ls')))
                  by reverse_append[U][reverse(ls')][node(n,empty)]
      ... = append(reverse(node(n,empty)), ls')
                  by rewrite goal with IH; reflexive
      ... = append(node(n,empty), ls') by reflexive
      ... = node(n,ls') by reflexive
  }
end

function map<T,U>(List<T>, fn T->U) -> List<U> {
  map(empty, f) = empty;
  map(node(x, ls), f) = node(f(x), map(ls, f));
}

theorem map_suc: map(L1, λx{ suc(x) } : fn Nat->Nat) = node(2, node(3, empty))
proof
  reflexive
end

theorem length_map: all T:type, f:fn T->T. all xs:List<T>.
  length(map(xs, f)) = length(xs)
proof
  arbitrary T:type, f:fn T->T;
  induction List<T>
  case empty {
    reflexive
  }
  case node(x, ls') {
    equations
      length(map(node(x,ls'),f))
          = suc(length(map(ls',f)))   by reflexive
      ... = suc(length(ls'))          by rewrite goal with IH; reflexive
      ... = length(node(x,ls'))       by reflexive
  }
end

theorem map_id: all T:type, f:fn T->T. if (all x:T. f(x) = x) then
   all xs:List<T>. map(xs, f) = xs
proof
  arbitrary T:type, f:fn T->T;
  assume fxx: (all x:T. f(x) = x);
  induction List<T>
  case empty {
    have map(empty,f) = empty by reflexive
  }
  case node(x, ls) {
    equations
      map(node(x,ls),f)
          = node(f(x), map(ls, f)) by reflexive
      ... = node(x, map(ls, f)) by rewrite goal with fxx[x]; reflexive
      ... = node(x,ls) by rewrite goal with IH; reflexive
  }
end

define ∘ : < T,U,V > fn (fn U->V), (fn T->U) -> (fn T->V)
         = generic T { λ g,f { λ x { g(f(x)) } } }

theorem compose_test: ((λx{x} : fn Nat -> Nat) ∘ (λx{x} : fn Nat -> Nat))(zero) = zero
proof
  reflexive
end

theorem map_append: all T:type, f: fn T->T, ys:List<T>. all xs:List<T>.
  map(append(xs,ys), f) = append(map(xs,f), map(ys, f))
proof
  arbitrary T:type, f:fn T->T, ys:List<T>;
  induction List<T>
  case empty {
    reflexive
  }
  case node(x, xs') {
    equations
      map(append(node(x,xs'),ys),f)
          = map(node(x, append(xs',ys)), f)     by reflexive
      ... = node(f(x), map(append(xs',ys), f))  by reflexive
      ... = node(f(x), append(map(xs',f), map(ys,f)))
                                        by (rewrite goal with IH; reflexive)
      ... = append(map(node(x,xs'),f),map(ys,f)) by reflexive
  }
end

theorem map_compose: all T:type, U:type, V:type, f:fn T->U, g:fn U->V. all ls :List<T>.
  map(map(ls, f), g) = map(ls, g ∘ f)
proof
  arbitrary T:type, U:type, V:type, f:fn T->U, g:fn U->V;
  induction List<T>
  case empty { reflexive }
  case node(x, ls) {
    equations
    map(map(node(x,ls),f),g)
	= node(g(f(x)), map(map(ls, f), g))  by reflexive
    ... = node(g(f(x)), map(ls, g ∘ f))      by rewrite goal with IH; reflexive
    ... = map(node(x,ls), g ∘ f)             by reflexive
  }
end

function foldr<T,U>(List<T>, U, fn T,U->U) -> U {
  foldr(empty, u, c) = u;
  foldr(node(t, ls), u, c) = c(t, foldr(ls, u, c));
}

theorem foldr_add12: foldr(L1, 0, λa,b{ a + b }) = 3
proof
  reflexive
end

theorem foldr_id: foldr(L1, empty, λx,ls{ node(x,ls) }) = L1
proof
  reflexive
end

function foldl<T,U>(List<T>, U, fn U,T->U) -> U {
  foldl(empty, u, c) = u;
  foldl(node(t, ls), u, c) = foldl(ls, c(u, t), c);
}

theorem foldl_add12: foldl(L1, 0, λa,b{ a + b }) = 3
proof
  reflexive
end  

theorem foldl_rev12:
  foldl(L1, empty, λls,x{ node(x,ls) }) = node(2,node(1,empty))
proof
  reflexive
end  


function down_from(Nat) -> List<Nat> {
  down_from(zero) = empty;
  down_from(suc(n)) = node(n, down_from(n));
}

theorem down_from2: down_from(2) = node(1, node(0, empty))
proof
  reflexive
end

define up_to : fn Nat -> List<Nat> = λ n { reverse(down_from(n)) }

theorem up_to2: up_to(2) = node(0, node(1, empty))
proof
  reflexive
end

theorem up_to3: up_to(3) = node(0, node(1, node(2, empty)))
proof
  reflexive
end

function range_aux(Nat, Nat, Nat) -> List<Nat> {
  range_aux(0, end, k) = map(up_to(end), (λ x { x + k }) : fn Nat->Nat);
  range_aux(suc(begin'), end, k) =
    switch end {
      case zero { empty } /* shouldn't happen */
      case suc(end') { range_aux(begin', end', suc(k)) }
    };
}

define range : fn Nat,Nat -> List<Nat> = λ b, e { range_aux(b, e, 0) }

theorem range_empty: range(0, 0) = empty
proof
  reflexive
end  

theorem range1: range(0, 1) = node(0, empty)
proof
  reflexive
end  

theorem range2: range(0, 2) = node(0, node(1, empty))
proof
  reflexive
end  

theorem range13: range(1, 3) = node(1, node(2, empty))
proof
  reflexive
end  

theorem range25: range(2, 5) = node(2, node(3, node(4, empty)))
proof
  reflexive
end  

union Pair<T,U> {
  pair(T,U);
}

function first<T,U>(Pair<T,U>) -> T {
  first(pair(x,y)) = x;
}

function second<T,U>(Pair<T,U>) -> U {
  second(pair(x,y)) = y;
}

function zip<T,U>(List<T>, List<U>) -> List< Pair<T, U> > {
  zip(empty, ys) = empty;
  zip(node(x, xs'), ys) =
    switch ys {
      case empty { empty }
      case node(y, ys') { node(pair(x,y), zip(xs', ys')) }
    };
}

theorem zip_id_right: all T:type. all xs:List<T>. zip(xs, empty) = empty
proof
  arbitrary T:type;
  induction List<T>
  case empty { reflexive }
  case node(x, xs') { reflexive }
end

define pairfun : < T1,T2,U1,U2 > fn (fn T1->T2), (fn U1->U2) -> (fn Pair<T1,U1> -> Pair<T2,U2>)
               = generic T1,T2,U1,U2 { λ f, g { λp{ pair(f(first(p)), g(second(p))) } } }

theorem zip_map: all T1:type, T2:type, U1:type, U2:type,
  f : fn T1 -> T2, g : fn U1 -> U2.
  all xs:List<T1>. all ys:List<U1>.
  zip(map(xs, f), map(ys, g)) = map(zip(xs,ys), pairfun(f,g))
proof
  arbitrary T1:type, T2:type, U1:type, U2:type, f : fn T1 -> T2, g : fn U1 -> U2;
  induction List<T1>
  case empty {
      arbitrary ys:List<U1>;
      reflexive
  }
  case node(x, xs') {
    arbitrary ys:List<U1>;
    switch ys {
    case empty { rewrite goal with EQ; reflexive }
    case node(y, ys') {
      equations
        zip(map(node(x,xs'),f),map(node(y,ys'),g))
	  = node(pair(f(x), g(y)), zip(map(xs',f), map(ys',g)))
	      by reflexive
      ... = node(pair(f(x), g(y)), map(zip(xs',ys'), pairfun(f,g)))
              by rewrite goal with IH[ys']; reflexive
      ... = map(zip(node(x,xs'),node(y,ys')), pairfun(f,g))
              by reflexive
    }
    }
  }
end