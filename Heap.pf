/*
  This is inspired by the paper
  A Functional Approach to Standard Binary Heaps
  by Vladimir Kostyukov.

  This is a min-heap.
*/

import Nat
import Pair

union Direction {
  go_left;
  go_right;
}

union Heap {
  empty_heap;
  heap_node(Heap, Nat, Direction, Heap);
}

function bubble_up_left(Heap,Nat,Direction,Heap) -> Heap {
  bubble_up_left(empty_heap, x, d1, R) =
      heap_node(empty_heap, x, d1, R);
  bubble_up_left(heap_node(LL, y, d2, LR), x, d1, R) =
    if y < x then
      heap_node(heap_node(LL, x, d2, LR), y, d1, R)
    else
      heap_node(heap_node(LL, y, d2, LR), x, d1, R);
}

// Note the flip of the argument order!
function bubble_up_right(Heap,Nat,Direction,Heap) -> Heap {
  bubble_up_right(empty_heap, x, d1, L) =
    heap_node(L, x, d1, empty_heap);
  bubble_up_right(heap_node(RL, y, d2, RR), x, d1, L) =
    if y < x then
      heap_node(L, y, d1, heap_node(RL, x, d2, RR))
    else
      heap_node(L, x, d1, heap_node(RL, y, d2, RR));
}

// The insert(H,x) function creates a new heap that includes all the
// keys in the heap H as well as the key x. It returns the new heap
// and a boolean that indicates whether the new heap is a perfect
// binary tree.

function insert(Heap,Nat) -> Pair<Heap,bool> {
  insert(empty_heap, x) =
    pair(heap_node(empty_heap, x, go_left, empty_heap), true);
  insert(heap_node(L, y, d, R), x) =
    let y' = if x < y then x else y;
    let x' = if x < y then y else x;
    switch d {
      case go_left {
	let p = insert(L, x');
        let dir = if second(p) then go_right else go_left;
        pair(heap_node(first(p), y', dir, R), false)	
      }
      case go_right {
	let p = insert(R, x');
        let dir = if second(p) then go_left else go_right;
        pair(heap_node(L, y', dir, first(p)), second(p))
      }
    };
}

// pop : Heap -> Pair<Heap, Nat>

// Delete the last node of a perfect tree.
function del_last(Heap) -> Pair<Heap,Nat> {
  del_last(empty_heap) = pair(empty_heap, 0); // can't happen
  del_last(heap_node(L, y, d, R)) =
    switch R {
      case empty_heap {
        pair(empty_heap, y)
      }
      case heap_node(RL, z, d2, RR) {
        let p = del_last(R);
	pair(heap_node(L, y, go_right, first(p)), second(p))
      }
    };
}

function heap_greater_equal(Heap, Nat) -> bool {
  heap_greater_equal(empty_heap, p) = true;
  heap_greater_equal(heap_node(L, c, d, R), p) = p ≤ c;
}

function is_heap(Heap) -> bool {
  is_heap(empty_heap) = true;
  is_heap(heap_node(L, x, d, R)) =
    is_heap(L) and is_heap(R) and
    heap_greater_equal(L, x) and heap_greater_equal(R, x);
}

function height(Heap) -> Nat {
  height(empty_heap) = 0;
  height(heap_node(L, x, d, R)) = suc(max(height(L), height(R)));
}

function is_perfect(Heap) -> bool {
  is_perfect(empty_heap) = true;
  is_perfect(heap_node(L, x, d, R)) =
    height(L) = height(R) and is_perfect(L) and is_perfect(R);
}

//  Def. A jagged tree is a complete but not perfect binary tree.
function is_jagged(Heap) -> bool {
  is_jagged(empty_heap) = false;
  is_jagged(heap_node(L, x, d, R)) =
    switch d {
      case go_left {
        is_jagged(L) and is_perfect(R) and height(L) = suc(height(R))
      }
      case go_right {
        is_perfect(L) and
	((is_jagged(R) and height(L) = height(R))
	or (is_perfect(R) and height(L) = suc(height(R))))
      }
    };
}

function is_complete(Heap) -> bool {
  is_complete(empty_heap) = true;
  is_complete(heap_node(L, x, d, R)) =
    switch d {
      case go_left {
        ((is_perfect(L) and height(L) = height(R))
	or (is_jagged(L) and height(L) = suc(height(R))))
        and is_perfect(R)
      }
      case go_right {
        is_perfect(L)
	and ((is_perfect(R) and height(L) = suc(height(R)))
	    or (is_jagged(R) and height(L) = height(R)))
      }
    };
/*
    (height(L) = height(R) and is_perfect(L) and is_complete(R))
    or (height(L) = suc(height(R)) and is_complete(L) and is_perfect(R));
*/
}


//---------------- Testing insert ----------------------------------------------

define H1 = heap_node(empty_heap, 1, go_left, empty_heap)
define H2 = heap_node(empty_heap, 2, go_left, empty_heap)
define H12 = heap_node(H2, 1, go_right, empty_heap)
define H3 = heap_node(empty_heap, 3, go_left, empty_heap)
define H123 = heap_node(H2, 1, go_left, H3)
define H4 = heap_node(empty_heap, 4, go_left, empty_heap)
define H24 = heap_node(H4, 2, go_right, empty_heap)
define H1234 = heap_node(H24, 1, go_left, H3)
define H142 = heap_node(H4, 1, go_left, H2)
define H01234 = heap_node(H142, 0, go_right, H3)

theorem insert_H1: insert(empty_heap, 1) = pair(H1, true)
proof
  apply H1, insert in goal; reflexive
end

theorem insert_H12: insert(H1, 2) = pair(H12, false)
proof
  apply H1, H2, H12, insert, bubble_up_left, first, second, <, ≤ in goal;
  reflexive
end

theorem insert_H123: insert(H12, 3) = pair(H123, true)
proof
  apply H2, H3, H12, H123, insert, bubble_up_right, first, second, <, ≤ in goal;
  reflexive
end

theorem heap_H123: is_heap(H123)
proof
  apply H1, H2, H3, H123, is_heap, heap_greater_equal, ≤ in goal; .
end

theorem insert_H1234: insert(H123, 4) = pair(H1234, false)
proof
  apply H2, H3, H4, H24, H123, H1234, insert, bubble_up_left,
        first, second, <, ≤ in goal;
  reflexive
end

theorem heap_H1234: is_heap(H1234)
proof
  apply H2, H3, H4, H24, H123, H1234, is_heap, heap_greater_equal, <, ≤ in goal;
  .
end

theorem insert_H01234: insert(H1234, 0) = pair(H01234, false)
proof
  apply H2, H3, H4, H24, H142, H123, H1234, H01234, insert,
        bubble_up_left, bubble_up_right,
        first, second, <, ≤ in goal;
  reflexive
end

theorem heap_H01234: is_heap(H01234)
proof
  apply H2, H3, H4, H24, H142, H123, H1234, H01234,
      is_heap, heap_greater_equal, <, ≤ in goal;
  .
end


//---------------- Testing del_last --------------------------------------------

theorem del_last_H1: del_last(H1) = pair(empty_heap, 1)
proof
  apply del_last, H1 in goal;
  reflexive
end

theorem del_last_h123: del_last(H123) = pair(H12, 3)
proof
  apply del_last, H1, H2, H3, H123, H12, first, second in goal;
  reflexive
end

//---------------- Correctness of insert ---------------------------------------

theorem heap_greater_equal_trans:
  all L:Heap, x:Nat, y:Nat.
  if heap_greater_equal(L, x) and y ≤ x then heap_greater_equal(L, y)
proof
  arbitrary L:Heap, x:Nat, y:Nat;
  assume prem: heap_greater_equal(L, x) and y ≤ x;
  switch L {
    case empty_heap {
      apply heap_greater_equal in goal;
      .
    }
    case heap_node(LL, z, d, LR) {
      apply heap_greater_equal in goal;
      have x_z: x ≤ z by apply heap_greater_equal in (_rewrite prem with EQ);
      have y ≤ z by using less_equal_trans[y][x,z] with (prem,x_z)
    }
  }
end
    
theorem heap_insert:
  all H: Heap. all x:Nat. 
  if is_heap(H)
  then is_heap(first(insert(H, x)))
       and (all z:Nat. if heap_greater_equal(H, z) and z ≤ x
                       then heap_greater_equal(first(insert(H,x)), z))
proof
  induction Heap
  case empty_heap {
    arbitrary x:Nat;
    assume prem;
    apply insert, first, is_heap, heap_greater_equal in goal;
    apply heap_greater_equal in goal;
    have (all z:Nat. if (z ≤ x) then z ≤ x)
        by (arbitrary z:Nat; assume zx : z ≤ x; zx)
  }
  case heap_node(L, y, d, R) {
    arbitrary x:Nat;
    assume prem: is_heap(heap_node(L,y,d,R));
    have is_heap_L: is_heap(L) by apply is_heap in prem;
    have is_heap_R: is_heap(R) by apply is_heap in prem;
    have L_y: heap_greater_equal(L,y) by apply is_heap in prem;
    have R_y: heap_greater_equal(R,y) by apply is_heap in prem;
    have is_heap_L_y: is_heap(first(insert(L, y)))
        by using (conjunct 0 of IH)[y] with is_heap_L;
    have is_heap_R_y: is_heap(first(insert(R, y)))
        by using (conjunct 1 of IH)[y] with is_heap_R;
    have is_heap_L_x: is_heap(first(insert(L, x)))
        by using (conjunct 0 of IH)[x] with is_heap_L;
    have is_heap_R_x: is_heap(first(insert(R, x)))
        by using (conjunct 1 of IH)[x] with is_heap_R;
    apply insert in goal;
    switch d {
      case go_left {
	apply first, is_heap in goal;
	switch x < y {
	  case true {
	    have x_l_y: x < y by (_rewrite goal with EQ; .);
	    have x_y: x ≤ y by using less_implies_less_equal[x][y] with x_l_y;
	    have L_x: heap_greater_equal(L,x) 
  	        by using heap_greater_equal_trans[L, y, x] with (L_y, x_y);
	    have R_x: heap_greater_equal(R,x)
  	        by using heap_greater_equal_trans[R, y, x] with (R_y, x_y);
	    have Ly_x: heap_greater_equal(first(insert(L,y)),x)
	        by using (conjunct 1 of (using (conjunct 0 of IH)[y]
					 with (is_heap_L)))[x]
	           with (L_x, x_y);
	    have zx_zx: (all z:Nat. if (z ≤ y) and (z ≤ x) then z ≤ x)
			    by (arbitrary z:Nat; assume p; p);
	    apply heap_greater_equal in goal;
	    (is_heap_L_y, is_heap_R, Ly_x, R_x, zx_zx)
	  }
	  case false {
	    apply heap_greater_equal in goal;
	    have y_x: y ≤ x by using not_less_less_equal[x][y]
     	                       with (assume xy: x < y; _rewrite xy with EQ);
	    have Lx_y: heap_greater_equal(first(insert(L,x)),y)
	      by using (conjunct 1 of (using (conjunct 0 of IH)[x]
				       with is_heap_L))[y]
   	         with (L_y, y_x);
            have zy_zy: all z:Nat. (if (z ≤ y) and (z ≤ x) then z ≤ y)
	      by (arbitrary z:Nat; assume p: z ≤ y and z ≤ x; p);
	    (is_heap_R, is_heap_L_x, Lx_y, R_y, zy_zy)
	  }
	}
      }
      case go_right {
	apply first, is_heap in goal;
	switch x < y {
	  case true {
	    have x_l_y: x < y by (_rewrite goal with EQ; .);
	    have x_y: x ≤ y by using less_implies_less_equal[x][y] with x_l_y;
	    have L_x: heap_greater_equal(L,x)
	      by using heap_greater_equal_trans[L, y, x] with (L_y, x_y);
	    have R_x: heap_greater_equal(R,x)
  	        by using heap_greater_equal_trans[R, y, x] with (R_y, x_y);
	    have Ry_x: heap_greater_equal(first(insert(R,y)),x)
	        by using (conjunct 1 of (using (conjunct 1 of IH)[y]
					 with (is_heap_R)))[x]
	           with (R_x, x_y);
            apply heap_greater_equal in goal;
	    have zx_zx: (all z:Nat. (if (z ≤ y) and (z ≤ x) then z ≤ x))
	        by (arbitrary z: Nat; assume p; p);
	    (is_heap_L, is_heap_R_y, L_x, Ry_x, zx_zx)
	  }
	  case false {
	    apply heap_greater_equal in goal;
	    have y_x: y ≤ x by using not_less_less_equal[x][y]
     	                       with (assume xy: x < y; _rewrite xy with EQ);
            have Rx_y: heap_greater_equal(first(insert(R,x)),y)
	      by using (conjunct 1 of (using (conjunct 1 of IH)[x]
				       with is_heap_R))[y]
   	         with (R_y, y_x);
            have zy_zy: all z:Nat. (if (z ≤ y) and (z ≤ x) then z ≤ y)
	      by (arbitrary z:Nat; assume p: z ≤ y and z ≤ x; p);
	    (is_heap_L, is_heap_R_x, L_y, Rx_y, zy_zy)
	  }
	}
      }
    }
  }
end

theorem perfect_insert_height:
  all H: Heap. all x:Nat. 
  if is_perfect(H)
  then height(first(insert(H, x))) = suc(height(H))
proof
  induction Heap
  case empty_heap {
    arbitrary x:Nat;
    apply is_perfect, insert, height in goal;
    apply insert, first, height, suc, max in goal;
    reflexive
  }
  case heap_node(L, x, d, R) {
    switch d {
      case go_left {
        arbitrary y:Nat;
	assume prem;
	apply insert, first, height in goal;
	have pL: is_perfect(L) by apply is_perfect in prem;
	have hLhR: height(L) = height(R) by apply is_perfect in prem;
	have hiL:
	   height(first(insert(L, if y < x then x else y))) = suc(height(L))
	   by (using (conjunct 0 of IH)[if y < x then x else y] with pL);
	_rewrite goal with hiL;
	_rewrite goal with hLhR;
	_rewrite goal with max_suc[height(R)];
        reflexive
      }
      case go_right {
        ?
      }
    }
  }
end

/*
theorem perfect_insert:
  all H: Heap. all x:Nat. 
  if is_complete(H) and second(insert(H, x))
  then is_perfect(first(insert(H, x)))
proof
  induction Heap
  case empty_heap {
    arbitrary x:Nat;
    apply is_complete, insert, first, second, is_perfect, height in goal;
    // It shouldn't be necessary to do another apply! -Jeremy
    apply insert, first, is_perfect, height in goal;
    reflexive
  }
  case heap_node(L, y, d, R) {
    arbitrary x:Nat;
    assume prem: is_complete(heap_node(L, y, d, R))
                 and second(insert(heap_node(L, y, d, R), x));
    switch d {
      case go_left {
        apply insert, is_complete, first, is_perfect in goal;
        switch x < y {
	  case true {
	    ?
	  }
	  case false {
	    ?
	  }
	}
      }
      case go_right {
        ?
      }
    }
  }
end

theorem complete_insert:
  all H: Heap. all x:Nat. 
  if is_complete(H)
  then is_complete(first(insert(H, x)))
proof
  ?
end
*/